extends KinematicBody2D


const EnemyDeathEffect = preload("res://Effects/EnemyDeathEffect.tscn")
const PotionDrop = preload("res://Drops/PotionDrop.tscn")


export var ACCELERATION = 300
export var MAX_SPEED = 50
export var FRICTION = 200
export var WANDER_TARGET_RANGE = 4

enum{
	IDLE,
	WANDER,
	CHASE
}

var velocity  = Vector2.ZERO

var knockback = Vector2.ZERO

var state = CHASE

onready var stats = $Stats
onready var playerDetectionZone = $PlayerDetectionZone
onready var sprite = $AnimatedSprite
onready var hurtbox = $Hurtbox 
onready var softCollision = $SoftCollision
onready var wandererController = $WanderController
onready var animationPlayer = $AnimationPlayer


func _ready():
	randomize()
	state = pick_random_state([IDLE,WANDER])
	animationPlayer.play("Stop")
func _physics_process(delta):
	knockback = knockback.move_toward(Vector2.ZERO,FRICTION * delta)
	knockback = move_and_slide(knockback)

	match state:
		IDLE:
			velocity = velocity.move_toward(Vector2.ZERO, FRICTION * delta)
			seek_player()
			if wandererController.get_time_left() == 0:
				update_wander()
		WANDER:
			seek_player()
			if wandererController.get_time_left() == 0:
				update_wander()
			accelerate_towards_point(wandererController.target_position, delta)

			if global_position.distance_to(wandererController.target_position) <= WANDER_TARGET_RANGE:
				update_wander()
		CHASE:
			var player = playerDetectionZone.player
			if player != null:
				accelerate_towards_point(player.global_position,delta)
				
			else:
				state = IDLE

			
			if softCollision.is_colliding():
				velocity += softCollision.get_push_vector() * delta * 400
	velocity = move_and_slide(velocity)

func update_wander():
	state = pick_random_state([IDLE,WANDER])
	wandererController.start_wander_timer(rand_range(1,3))

func accelerate_towards_point(position, delta):
	var direction = global_position.direction_to(position)
	velocity = velocity.move_toward(direction * MAX_SPEED, ACCELERATION * delta)
	sprite.flip_h = velocity.x < 0
	
func seek_player():
	if playerDetectionZone.can_see_player():
		state = CHASE

func pick_random_state(state_list):
	state_list.shuffle()
	return state_list.pop_front()
	


func _on_Hurtbox_area_entered(area):
	stats.health -= area.damage
	knockback = area.knockback_vector * 100
	hurtbox.start_invincibilty(0.4)
	hurtbox.create_hit_effect()
	

func _on_Stats_no_health():
	queue_free()
	var enemyDeathEffect = EnemyDeathEffect.instance()
	get_parent().add_child(enemyDeathEffect)
	enemyDeathEffect.global_position = global_position
	
	var percent = randf()
	if (percent < 0.7):
		var itemDrop = PotionDrop.instance()
		get_parent().call_deferred("add_child",itemDrop)
		itemDrop.global_position = global_position
		
		
	
	
func _on_Hurtbox_invincibility_started():
	animationPlayer.play("Start")

func _on_Hurtbox_invincibility_ended():
	animationPlayer.play("Stop")



